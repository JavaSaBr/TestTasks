plugins {
  id 'base'
  id 'jacoco-report-aggregation'
}

dependencies {
  jacocoAggregation projects.deviceModel
  jacocoAggregation projects.deviceRest
  jacocoAggregation projects.deviceService
}

reporting {
  reports {
    testCodeCoverageReport(JacocoCoverageReport) {
      testType = TestSuiteType.UNIT_TEST
    }
  }
}

tasks.named('check') {
  dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

tasks.named("testCodeCoverageReport").configure {
  it.doLast {

    def buildDir = layout.buildDirectory.asFile.get().toPath()
    def resultFile = buildDir.resolve("reports")
        .resolve("jacoco")
        .resolve("testCodeCoverageReport")
        .resolve("html")
        .resolve("index.html")

    println("Aggregated code coverage report: file://${resultFile}")
  }
}